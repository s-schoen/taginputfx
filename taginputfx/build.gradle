plugins {
    id 'java-library'
    id 'maven-publish'
    id 'maven'
    id 'org.openjfx.javafxplugin' version '0.0.9'
    id "com.jfrog.bintray" version "1.8.5"
}

def PUBLISH_VERSION = '1.0.0'

dependencies {
    // Use JUnit test framework
    testImplementation 'junit:junit:4.13'
}

javafx {
    version = '13'
    modules = [ 'javafx.controls']
}

if (!project.hasProperty("BINTRAY_KEY")) {
    ext.BINTRAY_KEY = ""
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    dryRun = false
    pkg {
        repo = 'JavaLibs'
        name = 'TagInputFx'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/s-schoen/taginputfx.git'
        version {
            name = PUBLISH_VERSION
            vcsTag = PUBLISH_VERSION
            released  = new Date()
        }
    }

    publications = ['TagInputFx']
}

def pomConfig = {
    licenses {
        license {
            name "MIT"
            url "https://opensource.org/licenses/MIT"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "steffen"
            name "Steffen S."
            email "steffen1995@gmail.com"
        }
    }

    scm {
        url "https://github.com/s-schoen/taginputfx"
    }
}

publishing {
    publications {
        TagInputFx(MavenPublication) {
            groupId = 'com.gmail.steffen1995'
            artifactId = 'taginputfx'
            version = PUBLISH_VERSION
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'A simple tag input control for JavaFx')
                root.appendNode('name', 'TagInputFx')
                root.children().last() + pomConfig
            }

            from components.java
        }
    }
}